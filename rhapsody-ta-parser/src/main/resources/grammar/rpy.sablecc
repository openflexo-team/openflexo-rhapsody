Package org.openflexo.ta.rhapsody.parser;

Helpers

  unicode_input_character = [0..0xffff];
  ht  = 0x0009;
  lf  = 0x000a;
  ff  = 0x000c;
  cr  = 0x000d;
  sp  = ' ';

  line_terminator = lf | cr | cr lf;
  input_character = [unicode_input_character - [cr + lf]];

  upper_case = ['A' .. 'Z'] | [0x00c0 .. 0x00dd] | 0x20ac;
  lower_case = ['a' .. 'z'] | [0x00e0 .. 0x00fd] | 0x20ac;
  digit = ['0'..'9'];
  hex_digit = ['0'..'9'] | ['a'..'f'] | ['A'..'F'];
  letter = upper_case | lower_case | '_' ;
  any        = [0 .. 0xffff];
  not_hyphen = [any - '"'];
  not_hyphen2 = [any - ''' ];

  minus = '-';

  decimal_numeral = minus? digit (digit)*;
  lit_float = minus? digit (digit)* '.' digit (digit)* (('E' | 'e') ('+' | '-')? digit (digit)*)?;

  escape_sequence = '\b' | '\t' | '\n' | '\f' | '\r' | '\"' | '\' ''' | '\\';
  string_character = [any - ['"' + '\']] | escape_sequence;

Tokens
  l_brc = '{';
  r_brc = '}';
  dot = '.';
  semi  = ';';
  colon = ':';
  minus = '-';
  assign = '=';
 
// keywords

version_token = 'version';
guid_token = 'GUID';

// literals
 
  decimal_numeral = decimal_numeral;
  lit_float = lit_float;
 
  hex_numeral = hex_digit+;
 
  string_value = '"' (string_character)* '"';
  chars_value = ''' (not_hyphen2)* ''';
  identifier = letter (letter | digit)*;

  white_space = (sp | ht | ff | line_terminator)*;

Ignored Tokens
  white_space;
 
Productions

  rpy_root = rpy_header rpy_complex_object;

  rpy_header = extended_identifier version_token version identifier decimal_numeral;

  rpy_single_object = 
    {string_value} string_value semi |
    {chars_value} chars_value semi |
    {time} time semi |
    {identifier} identifier semi |
    {none} semi |
    {style} string_value [s1]:decimal_numeral [s2]:decimal_numeral [s3]:decimal_numeral [s4]:decimal_numeral [s5]:decimal_numeral semi |
    {numbers} [n_list]:number* semi |
    {id_value} guid_token extended_hex_identifier semi |
    {rpy_complex_object} rpy_complex_object;

  number =
    {int} decimal_numeral |
    {float} lit_float;

  rpy_object = 
    {single_object} rpy_single_object |
    {object_list} rpy_single_object rpy_object;

  rpy_complex_object = l_brc identifier [property_values]:rpy_property_value* r_brc;

  rpy_property_value = minus identifier assign rpy_object;

  extended_identifier = 
    {identifier} identifier |
  	{tail} identifier minus extended_identifier;
  	
  version = 
    {decimal_numeral} decimal_numeral |
    {lit_float} lit_float |
  	{tail1} decimal_numeral dot version |
  	{tail2} lit_float dot version;
 
  extended_hex_identifier = 
    {hex_numeral} hex_numeral |
    {decimal_numeral} decimal_numeral |
  	{tail1} hex_numeral minus extended_hex_identifier|
  	{tail2} decimal_numeral minus extended_hex_identifier |
  	{tail3} hex_numeral extended_hex_identifier|
  	{tail4} decimal_numeral extended_hex_identifier;

  time = [day_month]:lit_float [d2]:dot [year]:decimal_numeral [c1]:colon [c2]:colon [hour]:decimal_numeral [c3]:colon [minut]:decimal_numeral [c4]:colon [second]:decimal_numeral;

 