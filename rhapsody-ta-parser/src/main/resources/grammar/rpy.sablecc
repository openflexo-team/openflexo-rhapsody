Package org.openflexo.ta.rhapsody.parser;

Helpers

  unicode_input_character = [0..0xffff];
  ht  = 0x0009;
  lf  = 0x000a;
  ff  = 0x000c;
  cr  = 0x000d;
  sp  = ' ';

  line_terminator = lf | cr | cr lf;
  input_character = [unicode_input_character - [cr + lf]];

  upper_case = ['A' .. 'Z'] | [0x00c0 .. 0x00dd] | 0x20ac;
  lower_case = ['a' .. 'z'] | [0x00e0 .. 0x00fd] | 0x20ac;
  digit = ['0'..'9'];
  hex_digit = ['0'..'9'] | ['a'..'f'] | ['A'..'F'];
  letter = upper_case | lower_case | '_' ;
  any        = [0 .. 0xffff];
  not_hyphen = [any - '"'];
  not_hyphen2 = [any - ''' ];

  minus = '-';

  integer_literal = minus? digit (digit)*;
  float_literal = minus? digit (digit)* '.' digit (digit)* (('E' | 'e') ('+' | '-')? digit (digit)*)?;

  escape_sequence = '\b' | '\t' | '\n' | '\f' | '\r' | '\"' | '\' ''' | '\\';
  string_character = [any - ['"' + '\']] | escape_sequence;

Tokens
  l_brc = '{';
  r_brc = '}';
  dot = '.';
  semi  = ';';
  colon = ':';
  minus = '-';
  assign = '=';
 
  // keywords

  version_token = 'version';
  guid_token = 'GUID';

  // literals
 
  integer_literal = integer_literal;
  float_literal = float_literal;
  hex_literal = hex_digit+;
 
  string_literal = '"' (string_character)* '"';
  chars_literal = ''' (not_hyphen2)* ''';
  identifier = letter (letter | digit)*;

  white_space = (sp | ht | ff | line_terminator)*;

Ignored Tokens
  white_space;
 
Productions

  rpy_root = rpy_header rpy_complex_object;

  rpy_header = extended_identifier version_token version identifier integer_literal;

  rpy_single_object = 
    {none} semi |
    {string_literal} string_literal semi |
    {chars_literal} chars_literal semi |
    {time_literal} time_literal semi |
    {identifier} identifier semi |
    {numbers} [n_list]:number* semi |
    {style} string_literal [s1]:integer_literal [s2]:integer_literal [s3]:integer_literal [s4]:integer_literal [s5]:integer_literal semi |
    {id_value} guid_token extended_hex_identifier semi |
    {rpy_complex_object} rpy_complex_object;

  number =
    {int} integer_literal |
    {float} float_literal;

  rpy_object = 
    {single_object} rpy_single_object |
    {object_list} rpy_single_object rpy_object;

  rpy_complex_object = l_brc identifier [property_values]:rpy_property_value* r_brc;

  rpy_property_value = minus identifier assign rpy_object;

  extended_identifier = 
    {identifier} identifier |
  	{tail} identifier minus extended_identifier;
  	
  version = 
    {integer_literal} integer_literal |
    {float_literal} float_literal |
  	{tail1} integer_literal dot version |
  	{tail2} float_literal dot version;
 
  extended_hex_identifier = 
    {hex_literal} hex_literal |
    {integer_literal} integer_literal |
  	{tail1} hex_literal minus extended_hex_identifier|
  	{tail2} integer_literal minus extended_hex_identifier |
  	{tail3} hex_literal extended_hex_identifier|
  	{tail4} integer_literal extended_hex_identifier;

  time_literal = [day_month]:float_literal [d2]:dot [year]:integer_literal [c1]:colon [c2]:colon [hour]:integer_literal [c3]:colon [minut]:integer_literal [c4]:colon [second]:integer_literal;

 